

package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	boolean errorDetected;
	Logger log = Logger.getLogger(getClass());
        
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }


:}

init with {:
	errorDetected = false;
:}


scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

terminal PROG, CONST, VOID, READ, PRINT, NEW, FINDANY;
terminal SEMICOLON, COMMA, DOT;
terminal ASSIGNMENT, MINUS, PLUS, MUL, DIV, MOD, INC, DEC;
terminal Integer NUMBER;
terminal Character CHAR_CONST;
terminal Boolean BOOL_CONST;
terminal String IDENT;
terminal LBRACE, RBRACE, LPAREN, RPAREN, LSQUARE, RSQUARE;

terminal EXTENDS, COLON, ELSE, IF, NOT_EQUAL, AND, EQUAL, LESS, LAMBDA, BREAK, GREATER, WHILE, GREATER_EQUAL, LESS_EQUAL, FOREACH, CLASS, RETURN, CONTINUE, OR;


nonterminal DeclList, Decl;
nonterminal MethodDeclList;
nonterminal ConstDecl, ConstDeclList, ConstDeclOne;
nonterminal VarDecl, VarList, VarOne, VarDeclList;
nonterminal Statement, StatementList, DesignatorStatement, DesignatorOptList, DesignatorOpt; 
nonterminal AssignOp, AddOp, MulOp;
nonterminal Label, OptMinus;
nonterminal VarDeclError, VarErrorList, DesignatorAssignOpError;
nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgName, Program, MethodDecl, MethodName, Designator, Expr, Term, Factor, NumConstOpt;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Type;


Program ::= (Program) PROG ProgName:p DeclList LBRACE MethodDeclList RBRACE;
ProgName ::=(ProgName) IDENT:progName;

DeclList ::= (DeclarationList) DeclList Decl
		|
		(NoDeclList) /* epsilon */
		;

Decl ::= (PConstDecl) ConstDecl 
		| 
		(PVarDecl)  VarDecl
		;


ConstDecl ::=(ConstDecl) CONST Type ConstDeclList SEMICOLON;

ConstDeclList ::= (ConstDeclListMore) ConstDeclList COMMA ConstDeclOne
				|
				(ConsDeclListOne) ConstDeclOne
				;
				
ConstDeclOne ::=(ConstNumber) IDENT:name AssignOp NUMBER:value
			|
			(ConstChar) IDENT:name AssignOp CHAR_CONST:value
			|
			(ConstBool) IDENT:name AssignOp BOOL_CONST:value
			;
			
				
VarDecl::= (VarDeclaration) Type:varType VarList SEMICOLON
		| 
		(VarDeclErrorSemi) Type:varType VarDeclError SEMICOLON
		;
		
VarDeclError ::= (VarDeclError)  error:e
						{: parser.report_error("Izvrsen oporavak u liniji " + eleft, null); :};

VarList::=(VarListMore) VarList COMMA VarOne
		|
		(VarListOne) VarOne
		|
		(VarListError) VarErrorList COMMA VarOne
		;
		
VarErrorList ::= (VarErrorList)  error:e
						{: parser.report_error("Izvrsen oporavak u liniji " + eleft, null); :};


VarOne::= (VarOneArray) IDENT:name LSQUARE RSQUARE
		| 
		(VarOneVar) IDENT:name
		;
		
VarDeclList ::= (VarDeclarationList) VarDeclList VarDecl
			|
			(NoVarDeclList) /* epsilon */
			;


Type ::= (Type) IDENT:typeName;

MethodDecl ::=(MethodDecl) VOID MethodName LPAREN RPAREN VarDeclList LBRACE StatementList RBRACE;

MethodName ::=(MethodName) IDENT:name;

MethodDeclList ::=(MethodDecls) MethodDeclList MethodDecl
				|
				(NoMethodDecls)  /* epsilon */
				;


Statement ::= (StatementDesignator) DesignatorStatement SEMICOLON
			|
			(StatementRead) READ LPAREN Designator RPAREN SEMICOLON
			|
			(StatementPrint) PRINT LPAREN Expr NumConstOpt RPAREN SEMICOLON
			|
			(ListOfStatements) LBRACE StatementList RBRACE
			|
			(StatementFindAny) Designator AssignOp Designator DOT FINDANY LPAREN Expr RPAREN SEMICOLON
			;


StatementList ::= (Statements) StatementList Statement
				|
				(NoStmt) /* epsilon */
				;
				


NumConstOpt ::= (NumConst) COMMA NUMBER:width
			|
			(NoNumConst) /* epsilon */
			;


DesignatorStatement ::=(DesignatorAssign) Designator AssignOp Expr
					|
					(DesignatorInc) Designator INC
					|
					(DesignatorDec) Designator DEC
					|
					(DesignatorStatementAssignError) Designator AssignOp DesignatorAssignOpError
					;

DesignatorAssignOpError ::= (DesignatorAssignOpError)  error:e
						{: parser.report_error("Izvrsen oporavak u liniji " + eleft, null); :};
					

Expr ::=(ExprPositive) Term
		|
		(ExprNegative) MINUS Term
		|
		(ExprAdd) Expr AddOp Term
		;



Term ::=(TermFactor) Factor
		|
		(TermMul) Term MulOp Factor
		;



Factor ::=(FactorDesignator) Designator
		|
		(FactorNumber) NUMBER:value
		|
		(FactorChar) CHAR_CONST:value
		|
		(FactorBool) BOOL_CONST:value
		|
		(FactorNewExpr) NEW Type LSQUARE Expr RSQUARE
		|
		(FactorExpr) LPAREN Expr RPAREN
		;
		
Designator ::= (DesignatorOne) IDENT:name
			|
			(DesignatorExpr) Designator LSQUARE Expr RSQUARE
			;

Label ::=(Label) IDENT;

AssignOp ::=(Assignment) ASSIGNMENT;

		
AddOp ::=(Plus) PLUS
		|
		(Minus) MINUS
		;
		
MulOp ::=(Mul) MUL
		|
		(Div) DIV
		|
		(Mod) MOD
		;


		

